buildscript {
    apply from: 'gradle/buildscript.gradle', to: buildscript
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.18"
    }
}


apply plugin: 'nebula-aggregate-javadocs'



allprojects {


    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'net.ltgt.apt'
    apply plugin: 'project-report'
    apply plugin: 'com.bmuschko.clover'
    apply plugin: "com.diffplug.gradle.spotless"
    apply plugin: 'io.spring.dependency-management'


    spotless {
        java {
            googleJavaFormat('1.5')
        }
    }

    clover {
        report {
            html = true
        }
    }


    def repos = [
            'http://artifacts.sunshower.io/repository/sunshower-releases',
            'http://artifacts.sunshower.io/repository/sunshower-cache',
            'http://artifacts.sunshower.io/repository/sunshower-snapshots',
    ]



    task testArtifact(type: Jar, dependsOn: testClasses) {
        classifier = 'tests'
        from sourceSets.test.output
    }


    task sourceJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }


    dependencies {
        clover 'org.openclover:clover:4.2.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        repos.each { repo ->
            maven { url "$repo" }
        }

        maven {
            url mavenRepositoryUrl

            credentials {
                username mavenRepositoryUsername
                password mavenRepositoryPassword
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom("io.sunshower.base:bom-imported:${version}") {
                bomProperties([
                        'env.version': '1.0.8.Final'
                ])
            }
        }
    }

    plugins.withType(JavaPlugin) {

        dependencies {
            annotationProcessor "org.projectlombok:lombok:1.18.2"
            compileOnly 'org.projectlombok:lombok:1.18.2'
            testCompileOnly 'org.projectlombok:lombok:1.18.2'
            compile 'javax.ws.rs:javax.ws.rs-api'
            compile 'org.eclipse.persistence:org.eclipse.persistence.moxy'
            compile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api'


            testCompile 'javax.mail:mail'
            testCompile('org.mockito:mockito-core')
            testCompile('org.junit.platform:junit-platform-launcher')
            testCompile('org.junit.platform:junit-platform-commons')
            testCompile('org.junit.jupiter:junit-jupiter-engine')
            testCompile('org.junit.vintage:junit-vintage-engine')

        }
    }


    publishing {
        repositories {
            maven {
                url mavenRepositoryUrl

                credentials {
                    username mavenRepositoryUsername
                    password mavenRepositoryPassword
                }
            }
        }

        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }

                artifact testArtifact {
                    classifier = 'tests'
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }

}


publishing {
    repositories {
        maven {
            url mavenRepositoryUrl

            credentials {
                username mavenRepositoryUsername
                password mavenRepositoryPassword
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifact archiveReports {
                classifier 'reports'
            }
        }
    }
}

task archiveReports(type: Zip) {
    from "$buildDir/docs"
    include "/**/*"

    from "$buildDir/reports/clover"

    archiveName 'reports.zip'
}

assemble.dependsOn cloverAggregateReports
assemble.dependsOn 'aggregateJavadocs'

assemble.dependsOn archiveReports
archiveReports.mustRunAfter cloverAggregateReports
archiveReports.mustRunAfter 'aggregateJavadocs'


defaultTasks 'clean', 'build', 'cloverAggregateReports', 'aggregateJavadocs'

